     
#define cubeThreadsP \
  for(int k=0;k<p_NqP;++k;@inner(2)) \
    for(int j=0;j<p_NqP;++j;@inner(1)) \
      for(int i=0;i<p_NqP;++i;@inner(0)) 

#if 0
// q  \in  Nq x Nq x Nq x Nelements
// Pq \in  NqPx NqPx NqPx Nelements

//                        |         | 
// form invP, P wrt   o x x x x x x x x o
//                        |         |

// 1D transforms (assume J=const)
//  S = (lambda*W + hScaling*trans(D)*W*D) // stiffness matrix (also needs assembly)
//
//  S~ = W\S = W\trans(D)*W*D
//  S~ = V*Mu*inv(V) [ [V,Mu] = eig(S~) ]
//
//  S = W*V*(lambda*I+hScaling*Mu)*inv(V)
//
// inv(S) = V*(lambda*I + hScaling*Mu)^{-1}*inv(W*V)
//
// invP := inv(W*V)
// P = V
//
// inv(S) = P*(lambda*I + hscaling*Mu)^{-1}*invP

// 1. multiply by invP in each direcction
// 2. divide nodewise by  (lambda + Mu_i/h_r^2 + Mu_j/h_s^2 + Mu_k/h_t^2)*h_r*h_s*h_t/8 for suitable h_r,h_s,h_t
//    use h_r = 1/min r_x ? give or take a factor of 2
// 3. multiply by P in each direction
#endif


@kernel void ellipticOasPreconHex3D(const int Nelements,
				   @restrict const  int   *  vmapPP,
				   @restrict const  int   *  faceNodesP,
				   @restrict const  dfloat *  forward,
				   @restrict const  dfloat *  diagInvOp,
				   @restrict const  dfloat *  back,
				   @restrict const  dfloat *  q,
				   @restrict dfloat *  Pq){
  
  for(int e=0; e<Nelements; ++e; @outer(0)){
    
    @shared dfloat s_q1[p_NqP][p_NqP][p_NqP];
    @shared dfloat s_q2[p_NqP][p_NqP][p_NqP];
    @shared dfloat s_q3[p_NqP][p_NqP][p_NqP];
    @shared dfloat s_forward[p_NqP][p_NqP];
    @shared dfloat s_back[p_NqP][p_NqP];
    @exclusive dfloat r_diagInvOp;
    
    cubeThreadsP{

      int n=i+p_NqP*j+p_NqP*p_NqP*k;

      s_q1[0][0][n] = 0; // zero @shared memory
    }

    @barrier("local");
    
    cubeThreadsP{

      int n=i+p_NqP*j+p_NqP*p_NqP*k;
#if 0
      if(n<p_Np){
	// coalesce reads from p_Nq x p_Nq x p_Nq storage
	int ip = n%p_Nq;
	int jp = (n/p_Nq)%p_Nq;
	int kp = (n/(p_Nq*p_Nq));
	s_q1[kp+1][jp+1][ip+1] = q[n+e*p_Np];
      }
#else
      if((k<(p_NqP-2)) && (j<(p_NqP-2)) && (i<(p_NqP-2))){
	int m = k*p_Nq*p_Nq + j*p_Nq + i;
	s_q1[k+1][j+1][i+1] = q[m+e*p_Np];
      }
#endif

      r_diagInvOp = diagInvOp[n+e*p_NpP];

      if(k==0) s_forward[j][i]  = forward[i+p_NqP*j];
      if(k==1) s_back[j][i]     = back[i+p_NqP*j];

      // thread safe since the overlap nodes do not overlap
      // p_NqP = (N+3)^3
      // p_Nfp*p_Nfaces = (N+1)*(N+1)*4 < (N+3)^3
      if(n<p_Nfp*p_Nfaces){
	const int id = vmapPP[n+e*p_Nfaces*p_Nfp];
	s_q1[0][0][faceNodesP[n]] = q[id];
      }
    }
      
    @barrier("local");

    // r-transform
    cubeThreadsP{
      dfloat tmp = 0.f;

      #pragma unroll p_NqP
	for(int n=0;n<p_NqP;++n)
	  tmp += s_forward[i][n]*s_q1[k][j][n];

      s_q2[k][j][i] = tmp;
    }

    @barrier("local");

    // s-transform
    cubeThreadsP{
      dfloat tmp = 0.f;

      #pragma unroll p_NqP
	for(int n=0;n<p_NqP;++n)
	  tmp += s_forward[j][n]*s_q2[k][n][i];
      
      s_q3[k][j][i]  = tmp;
    }

    @barrier("local");

    // t-transform 
    cubeThreadsP{
      dfloat tmp = 0.f;

      #pragma unroll p_NqP
	for(int n=0;n<p_NqP;++n)
	  tmp += s_forward[k][n]*s_q3[n][j][i];

      // diagonal inverse
      s_q1[k][j][i] = r_diagInvOp*tmp;
    }

    @barrier("local");

    // t-transform back
    cubeThreadsP{
      dfloat tmp = 0.f;

      #pragma unroll p_NqP
	for(int n=0;n<p_NqP;++n)
	  tmp += s_back[k][n]*s_q1[n][j][i];
      
      s_q2[k][j][i] = tmp; 
    }

    @barrier("local");
    
    // s-transform back
    cubeThreadsP{
      dfloat tmp = 0.f;

      #pragma unroll p_NqP
	for(int n=0;n<p_NqP;++n)
	  tmp += s_back[j][n]*s_q2[k][n][i];
      
      s_q3[k][j][i] = tmp;
    }

    @barrier("local");

    // r-transform back
    cubeThreadsP{
      dfloat tmp = 0.f;

      #pragma unroll p_NqP
	for(int n=0;n<p_NqP;++n)
	  tmp += s_back[i][n]*s_q3[k][j][n];
      
      const int id=i+p_NqP*j+p_NqP*p_NqP*k + p_NpP*e;
      Pq[id] = tmp;
    }
  }
}
					
  
