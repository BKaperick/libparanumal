
void boundaryConditions3D(int bc, dfloat time,
        dfloat x, dfloat y, dfloat z,
        dfloat uM, dfloat vM, dfloat wM, dfloat pM,
        dfloat *uP, dfloat *vP, dfloat *wP, dfloat *pP){

  if(bc==1){
    // assert Neumann for pressure and no penetration for velocity
    *uP = -uM;
    *vP = -vM;
    *wP = -wM;
    *pP = pM;
  }
  if(bc==2){
    dfloat dx = 1.f/sqrt(2.f);
    dfloat dy = 1.f/sqrt(2.f);
    dfloat dz = 0;
    dfloat omega = 10*M_PI;
    dfloat wave = cos(omega*(time-(x*dx+y*dy+z*dz)));
    dfloat uI = dx*wave;
    dfloat vI = dy*wave;
    dfloat wI = dz*wave;
    dfloat pI = wave;

    // scattered field wall
    *uP = -uM -2.f*uI;
    *vP = -vM -2.f*vI;
    *wP = -wM -2.f*wI;
    *pP = pM;
  }
}

@kernel void acousticsbbdgMRABPmlSurfaceP3D(const int pmlNelements,
           @restrict const  int   *  pmlElementIds,
           @restrict const  int   *  pmlIds,
           @restrict const  dfloat *  sgeo,
           @restrict const  int   *  L0ids,
           @restrict const  dfloat *  L0vals,
           @restrict const  int   *  ELids,
           @restrict const  dfloat *  ELvals,
           @restrict const  int   *  vmapM,
           @restrict const  int   *  mapP,
           @restrict const  int   *  EToB,
           const dfloat time,
           @restrict const  dfloat *  x,
           @restrict const  dfloat *  y,
           @restrict const  dfloat *  z,
           @restrict const  dfloat *  q,
           @restrict const  dfloat *  pmlq,
           @restrict const  dfloat *  fQM,
           @restrict const  dfloat *  fQP,
           @restrict dfloat       *  rhsq,
           @restrict dfloat       *  pmlrhsq,
           const int   rhsShift){

  // for all elements
  for(int eo=0;eo<pmlNelements;eo+=p_NblockS;@outer(0)){

    // @shared storage for flux terms
    @shared dfloat s_fluxu[p_NblockS][p_Nfp*p_Nfaces];
    @shared dfloat s_fluxv[p_NblockS][p_Nfp*p_Nfaces];
    @shared dfloat s_fluxw[p_NblockS][p_Nfp*p_Nfaces];
    @shared dfloat s_fluxpx[p_NblockS][p_Nfp*p_Nfaces];
    @shared dfloat s_fluxpy[p_NblockS][p_Nfp*p_Nfaces];
    @shared dfloat s_fluxpz[p_NblockS][p_Nfp*p_Nfaces];

    @shared dfloat s_fluxu_copy[p_NblockS][p_Nfp*p_Nfaces];
    @shared dfloat s_fluxv_copy[p_NblockS][p_Nfp*p_Nfaces];
    @shared dfloat s_fluxw_copy[p_NblockS][p_Nfp*p_Nfaces];
    @shared dfloat s_fluxpx_copy[p_NblockS][p_Nfp*p_Nfaces];
    @shared dfloat s_fluxpy_copy[p_NblockS][p_Nfp*p_Nfaces];
    @shared dfloat s_fluxpz_copy[p_NblockS][p_Nfp*p_Nfaces];

    // for all face nodes of all elements
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){ // maxNodes = max(Nfp*Nfaces,Np)
        const int et = eo + es;
        if(et<pmlNelements){

          if(n<p_Nfp*p_Nfaces){
            const int e = pmlElementIds[et];

            // find face that owns this node
            const int face = n/p_Nfp;

            // load surface geofactors for this face
            const int sid = p_Nsgeo*(e*p_Nfaces+face);
            const dfloat nx   = sgeo[sid+p_NXID];
            const dfloat ny   = sgeo[sid+p_NYID];
            const dfloat nz   = sgeo[sid+p_NZID];
            const dfloat sJ   = sgeo[sid+p_SJID];
            const dfloat invJ = sgeo[sid+p_IJID];

            // indices of negative and positive traces of face node
            const int id  = e*p_NfpMax*p_Nfaces + face*p_NfpMax + iid;
            const int idM = id*p_Nfields;
            const int idP = mapP[id]*p_Nfields;

            // load negative and positive trace node values of q
            const dfloat uM = fQM[idM+0], vM = fQM[idM+1], wM = fQM[idM+2], pM = fQM[idM+3];
                  dfloat uP = fQP[idP+0], vP = fQP[idP+1], wP = fQP[idP+2], pP = fQP[idP+3];

            // apply boundary condition
            const int bc = EToB[face+p_Nfaces*e];
            if(bc>0)  {
              const int idB = vmapM[id];
              boundaryConditions3D(bc, time, x[idB], y[idB], z[idB], uM, vM, wM, pM, &uP, &vP, &wP, &pP);
            }

            // compute (q^* - q^-)
            const dfloat duS = 0.5*(uP-uM) + p_Lambda2*(-nx)*(pP-pM);
            const dfloat dvS = 0.5*(vP-vM) + p_Lambda2*(-ny)*(pP-pM);
            const dfloat dwS = 0.5*(wP-wM) + p_Lambda2*(-nz)*(pP-pM);
            const dfloat dpS = 0.5*(pP-pM) + p_Lambda2*(-nx*(uP-uM)-ny*(vP-vM)-nz*(wP-wM));

            // evaluate "flux" terms: (sJ/J)*(A*nx+B*ny)*(q^* - q^-)
            s_fluxu[es][n] = invJ*sJ*(-nx*dpS);
            s_fluxv[es][n] = invJ*sJ*(-ny*dpS);
            s_fluxw[es][n] = invJ*sJ*(-nz*dpS);
            s_fluxpx[es][n] = invJ*sJ*(-nx*duS);
            s_fluxpy[es][n] = invJ*sJ*(-ny*dvS);
            s_fluxpz[es][n] = invJ*sJ*(-nz*dwS);
          }
        }
      }
    }

    // wait for all @shared memory writes of the previous inner loop to complete
    @barrier("local");

    // apply L0 to fluxes. use sparse format
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){
        int et = eo + es;
        if(et<pmlNelements){
          if(n<p_Nfp*p_Nfaces){

            const int id = n%p_Nfp;  // warning: redundant reads
            const int f  = n/p_Nfp;

            dfloat utmpflux = 0.0;
            dfloat vtmpflux = 0.0;
            dfloat wtmpflux = 0.0;
            dfloat pxtmpflux = 0.0;
            dfloat pytmpflux = 0.0;
            dfloat pztmpflux = 0.0;

            // sparse application of L0
            #pragma unroll 7
            for (int m = 0; m < 7; ++m){
              int   L0id  = L0ids [id + p_Nfp*m];
              dfloat L0val = L0vals[id + p_Nfp*m];

              utmpflux += L0val * s_fluxu[es][L0id+f*p_Nfp];
              vtmpflux += L0val * s_fluxv[es][L0id+f*p_Nfp];
              wtmpflux += L0val * s_fluxw[es][L0id+f*p_Nfp];
              pxtmpflux += L0val * s_fluxpx[es][L0id+f*p_Nfp];
              pytmpflux += L0val * s_fluxpy[es][L0id+f*p_Nfp];
              pztmpflux += L0val * s_fluxpz[es][L0id+f*p_Nfp];
            }

            s_fluxu_copy[es][n] = utmpflux;
            s_fluxv_copy[es][n] = vtmpflux;
            s_fluxw_copy[es][n] = wtmpflux;
            s_fluxpx_copy[es][n] = pxtmpflux;
            s_fluxpy_copy[es][n] = pytmpflux;
            s_fluxpz_copy[es][n] = pztmpflux;
          }
        }
      }
    }

    @barrier("local");

    // apply lift reduction and accumulate RHS
    for(int es=0;es<p_NblockS;++es;@inner(1)){
      for(int n=0;n<p_maxNodes;++n;@inner(0)){
        int et = eo + es;
        if(et<pmlNelements){
          if(n<p_Np){
            const int e  = pmlElementIds[et];
            const int id = 3*p_Nfields*(p_NpMax*e + n) + p_Nfields*rhsShift;
            const int pmlId  = pmlIds[et];
            const int pmlrhsId = 3*p_pmlNfields*(p_NpMax*pmlId + n) + p_pmlNfields*rhsShift;

            // load RHS
            dfloat rhsqnu = rhsq[id+0];
            dfloat rhsqnv = rhsq[id+1];
            dfloat rhsqnw = rhsq[id+2];

            dfloat rhsqnpx = pmlrhsq[pmlrhsId+0];
            dfloat rhsqnpy = pmlrhsq[pmlrhsId+1];
            dfloat rhsqnpz = pmlrhsq[pmlrhsId+2];

            // rhs += LIFT*((sJ/J)*(A*nx+B*ny)*(q^* - q^-))
            #pragma unroll p_max_EL_nnz
            for (int m = 0; m < p_max_EL_nnz; ++m){
              const int id = n + m*p_Np;
              const dfloat ELval = ELvals[id];
              const int   ELid  = ELids [id];
              rhsqnu += ELval * s_fluxu_copy[es][ELid];
              rhsqnv += ELval * s_fluxv_copy[es][ELid];
              rhsqnw += ELval * s_fluxw_copy[es][ELid];
              rhsqnpx += ELval * s_fluxpx_copy[es][ELid];
              rhsqnpy += ELval * s_fluxpy_copy[es][ELid];
              rhsqnpz += ELval * s_fluxpz_copy[es][ELid];
            }

            // store incremented rhs
            rhsq[id+0] = rhsqnu;
            rhsq[id+1] = rhsqnv;
            rhsq[id+2] = rhsqnw;

            pmlrhsq[pmlrhsId+0] = rhsqnpx;
            pmlrhsq[pmlrhsId+1] = rhsqnpy;
            pmlrhsq[pmlrhsId+2] = rhsqnpz;
          }
        }
      }
    }
  }
}
