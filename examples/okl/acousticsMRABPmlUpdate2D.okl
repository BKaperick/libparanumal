

@kernel void acousticsMRABPmlUpdate2D(const int pmlNelements,
           @restrict const  int   *  pmlElementIds,
           @restrict const  int   *  pmlIds,
           const dfloat dt,
           const dfloat a1,
           const dfloat a2,
           const dfloat a3,
           @restrict const  int   *  vmapM,
           @restrict const  dfloat *  rhsq,
           @restrict const  dfloat *  pmlrhsq,
           @restrict dfloat *  q,
           @restrict dfloat *  pmlq,
           @restrict dfloat *  fQM,
           @restrict dfloat *  fQP,
           const int   rhsShift){

  for(int et=0;et<pmlNelements;++et;@outer(0)){  // for all elements
    @shared dfloat s_q[p_Np*p_Nfields];

    @exclusive int e;

    for(int n=0; n < p_maxNodes; ++n; @inner(0)){
      e = pmlElementIds[et];
      if (n < p_Np){
        const int pmlId = pmlIds[et];

        const int id  = p_Nfields*(e*p_Np + n);
        const int pid = p_pmlNfields*(pmlId*p_Np + n);

        const int rhsId1 = 3*id + ((rhsShift+0)%3)*p_Nfields;
        const int rhsId2 = 3*id + ((rhsShift+1)%3)*p_Nfields;
        const int rhsId3 = 3*id + ((rhsShift+2)%3)*p_Nfields;
        const int pmlrhsId1 = 3*pid + ((rhsShift+0)%3)*p_pmlNfields;
        const int pmlrhsId2 = 3*pid + ((rhsShift+1)%3)*p_pmlNfields;
        const int pmlrhsId3 = 3*pid + ((rhsShift+2)%3)*p_pmlNfields;

        const dfloat px = pmlq[pid+0] + dt*(a1*pmlrhsq[pmlrhsId1+0] + a2*pmlrhsq[pmlrhsId2+0] + a3*pmlrhsq[pmlrhsId3+0]);
        const dfloat py = pmlq[pid+1] + dt*(a1*pmlrhsq[pmlrhsId1+1] + a2*pmlrhsq[pmlrhsId2+1] + a3*pmlrhsq[pmlrhsId3+1]);

        s_q[p_Nfields*n+0] = q[id+0] + dt*(a1*rhsq[rhsId1+0] + a2*rhsq[rhsId2+0] + a3*rhsq[rhsId3+0]);
        s_q[p_Nfields*n+1] = q[id+1] + dt*(a1*rhsq[rhsId1+1] + a2*rhsq[rhsId2+1] + a3*rhsq[rhsId3+1]);
        s_q[p_Nfields*n+2] = px+py;

        //write update
        pmlq[pid+0] = px;
        pmlq[pid+1] = py;
        q[id+0] = s_q[p_Nfields*n+0];
        q[id+1] = s_q[p_Nfields*n+1];
        q[id+2] = s_q[p_Nfields*n+2];
      }
    }
    @barrier("local");

    for(int n=0; n < p_maxNodes; ++n; @inner(0)){
      if (n < p_Nfp*p_Nfaces){
        const int iid = e*p_Nfp*p_Nfaces + n;
        const int qidM = p_Nfields*(vmapM[iid]-e*p_Np);
        const int qid  = p_Nfields*iid;

        fQP[qid+0] = s_q[qidM+0];
        fQP[qid+1] = s_q[qidM+1];
        fQP[qid+2] = s_q[qidM+2];
        fQM[qid+0] = s_q[qidM+0];
        fQM[qid+1] = s_q[qidM+1];
        fQM[qid+2] = s_q[qidM+2];
      }
    }
  }
}

@kernel void acousticsMRABPmlTraceUpdate2D(const int pmlNelements,
           @restrict const  int   *  pmlElementIds,
           @restrict const  int   *  pmlIds,
           const dfloat dt,
           const dfloat a1,
           const dfloat a2,
           const dfloat a3,
           @restrict const  int   *  vmapM,
           @restrict const  dfloat *  rhsq,
           @restrict const  dfloat *  pmlrhsq,
           @restrict const  dfloat *  q,
           @restrict const  dfloat *  pmlq,
           @restrict dfloat *  fQM,
           @restrict dfloat *  fQP,
           const int   rhsShift){

  for(int et=0;et<pmlNelements;++et;@outer(0)){  // for all elements
    @shared dfloat s_q[p_Np*p_Nfields];

    @exclusive int e;

    for(int n=0; n < p_maxNodes; ++n; @inner(0)){
      e = pmlElementIds[et];
      if (n < p_Np){
        const int pmlId = pmlIds[et];

        const int id = p_Nfields*(e*p_Np + n);
        const int pid = p_pmlNfields*(pmlId*p_Np + n);

        const int rhsId1 = 3*id + ((rhsShift+0)%3)*p_Nfields;
        const int rhsId2 = 3*id + ((rhsShift+1)%3)*p_Nfields;
        const int rhsId3 = 3*id + ((rhsShift+2)%3)*p_Nfields;
        const int pmlrhsId1 = 3*pid + ((rhsShift+0)%3)*p_pmlNfields;
        const int pmlrhsId2 = 3*pid + ((rhsShift+1)%3)*p_pmlNfields;
        const int pmlrhsId3 = 3*pid + ((rhsShift+2)%3)*p_pmlNfields;

        const dfloat px = pmlq[pid+0] + dt*(a1*pmlrhsq[pmlrhsId1+0] + a2*pmlrhsq[pmlrhsId2+0] + a3*pmlrhsq[pmlrhsId3+0]);
        const dfloat py = pmlq[pid+1] + dt*(a1*pmlrhsq[pmlrhsId1+1] + a2*pmlrhsq[pmlrhsId2+1] + a3*pmlrhsq[pmlrhsId3+1]);

        s_q[p_Nfields*n+0] = q[id+0] + dt*(a1*rhsq[rhsId1+0] + a2*rhsq[rhsId2+0] + a3*rhsq[rhsId3+0]);
        s_q[p_Nfields*n+1] = q[id+1] + dt*(a1*rhsq[rhsId1+1] + a2*rhsq[rhsId2+1] + a3*rhsq[rhsId3+1]);
        s_q[p_Nfields*n+2] = px+py;
      }
    }
    @barrier("local");

    for(int n=0; n < p_maxNodes; ++n; @inner(0)){
      if (n < p_Nfp*p_Nfaces){
        const int iid = e*p_Nfp*p_Nfaces + n;
        const int qidM = p_Nfields*(vmapM[iid]-e*p_Np);
        const int qid  = p_Nfields*iid;

        fQP[qid+0] = s_q[qidM+0];
        fQP[qid+1] = s_q[qidM+1];
        fQP[qid+2] = s_q[qidM+2];
        fQM[qid+0] = s_q[qidM+0];
        fQM[qid+1] = s_q[qidM+1];
        fQM[qid+2] = s_q[qidM+2];
      }
    }
  }
}

@kernel void acousticsMRABPmlUpdate2D_wadg(const int pmlNelements,
           @restrict const  int   *  pmlElementIds,
           @restrict const  int   *  pmlIds,
           const dfloat dt,
           const dfloat a1,
           const dfloat a2,
           const dfloat a3,
           @restrict const  dfloat *  cubInterp,
           @restrict const  dfloat *  cubProject,
           @restrict const  dfloat *  c2,
           @restrict const  int   *  vmapM,
           @restrict dfloat *  rhsq,
           @restrict dfloat *  pmlrhsq,
           @restrict dfloat *  q,
           @restrict dfloat *  pmlq,
           @restrict dfloat *  fQM,
           @restrict dfloat *  fQP,
           const int   rhsShift){

  for(int et=0;et<pmlNelements;++et;@outer(0)){  // for all elements
    @shared dfloat s_q[p_Np*p_Nfields];
    @shared dfloat s_p[p_cubNp];

    for(int n=0; n < p_maxCubNodes; ++n; @inner(0)){
      if (n < p_Np){
        const int e = pmlElementIds[et];
        const int pmlId = pmlIds[et];

        const int id = p_Nfields*(e*p_Np + n);
        const int pid = p_pmlNfields*(pmlId*p_Np + n);

        const int rhsId1 = 3*id + ((rhsShift+0)%3)*p_Nfields;
        const int rhsId2 = 3*id + ((rhsShift+1)%3)*p_Nfields;
        const int rhsId3 = 3*id + ((rhsShift+2)%3)*p_Nfields;
        const int pmlrhsId1 = 3*pid + ((rhsShift+0)%3)*p_pmlNfields;
        const int pmlrhsId2 = 3*pid + ((rhsShift+1)%3)*p_pmlNfields;
        const int pmlrhsId3 = 3*pid + ((rhsShift+2)%3)*p_pmlNfields;

        const dfloat px = pmlq[pid+0] + dt*(a1*pmlrhsq[pmlrhsId1+0] + a2*pmlrhsq[pmlrhsId2+0] + a3*pmlrhsq[pmlrhsId3+0]);
        const dfloat py = pmlq[pid+1] + dt*(a1*pmlrhsq[pmlrhsId1+1] + a2*pmlrhsq[pmlrhsId2+1] + a3*pmlrhsq[pmlrhsId3+1]);

        s_q[p_Nfields*n+0] = q[id+0] + dt*(a1*rhsq[rhsId1+0] + a2*rhsq[rhsId2+0] + a3*rhsq[rhsId3+0]);
        s_q[p_Nfields*n+1] = q[id+1] + dt*(a1*rhsq[rhsId1+1] + a2*rhsq[rhsId2+1] + a3*rhsq[rhsId3+1]);
        s_q[p_Nfields*n+2] = px+py;

        //write update
        pmlq[pid+0] = px;
        pmlq[pid+1] = py;
        q[id+0] = s_q[p_Nfields*n+0];
        q[id+1] = s_q[p_Nfields*n+1];
      }
    }
    @barrier("local");

    // Interpolate p to cubature nodes
    for(int n=0;n<p_maxCubNodes;++n;@inner(0)){
      if (n<p_cubNp) {
        const int e = pmlElementIds[et];
        // Prefetch value of c2
        const dfloat c2n = c2[n + e*p_cubNp];

        s_p[n] = 0.f;
        for (int i=0;i<p_Np;++i){
          s_p[n] += cubInterp[n + i*p_cubNp] * s_q[p_Nfields*i+2];
        }
        // Multiply result by wavespeed c2 at cubature node
        s_p[n] *= c2n;
      }
    }
    @barrier("local");

    // Project result back down
    for(int n=0;n<p_maxCubNodes;++n;@inner(0)){
      if (n < p_Np){
        const int e = pmlElementIds[et];
        const int id = p_Nfields*(e*p_Np + n);

        // Project scaled rhs down
        dfloat c2p = 0.f;
        for (int i=0;i<p_cubNp;++i){
          c2p += cubProject[n + i*p_Np] * s_p[i];
        }

        s_q[p_Nfields*n+2] = c2p;
        q[id+2] = c2p;
      }
    }
    @barrier("local");

    for(int n=0; n < p_maxCubNodes; ++n; @inner(0)){
      if (n < p_Nfp*p_Nfaces){
        const int e = pmlElementIds[et];
        const int iid = e*p_Nfp*p_Nfaces + n;
        const int qidM = p_Nfields*(vmapM[iid]-e*p_Np);
        const int qid  = p_Nfields*iid;

        fQP[qid+0] = s_q[qidM+0];
        fQP[qid+1] = s_q[qidM+1];
        fQP[qid+2] = s_q[qidM+2];
        fQM[qid+0] = s_q[qidM+0];
        fQM[qid+1] = s_q[qidM+1];
        fQM[qid+2] = s_q[qidM+2];
      }
    }
  }
}

@kernel void acousticsMRABPmlTraceUpdate2D_wadg(const int pmlNelements,
           @restrict const  int   *  pmlElementIds,
           @restrict const  int   *  pmlIds,
           const dfloat dt,
           const dfloat a1,
           const dfloat a2,
           const dfloat a3,
           @restrict const  dfloat *  cubInterp,
           @restrict const  dfloat *  cubProject,
           @restrict const  dfloat *  c2,
           @restrict const  int   *  vmapM,
           @restrict const  dfloat *  rhsq,
           @restrict const  dfloat *  pmlrhsq,
           @restrict const  dfloat *  q,
           @restrict const  dfloat *  pmlq,
           @restrict dfloat *  fQM,
           @restrict dfloat *  fQP,
           const int   rhsShift){

  for(int et=0;et<pmlNelements;++et;@outer(0)){  // for all elements
    @shared dfloat s_q[p_Np*p_Nfields];
    @shared dfloat s_p[p_cubNp];

    for(int n=0; n < p_maxCubNodes; ++n; @inner(0)){
      if (n < p_Np){
        const int e = pmlElementIds[et];
        const int pmlId = pmlIds[et];

        const int id = p_Nfields*(e*p_Np + n);
        const int pid = p_pmlNfields*(pmlId*p_Np + n);

        const int rhsId1 = 3*id + ((rhsShift+0)%3)*p_Nfields;
        const int rhsId2 = 3*id + ((rhsShift+1)%3)*p_Nfields;
        const int rhsId3 = 3*id + ((rhsShift+2)%3)*p_Nfields;
        const int pmlrhsId1 = 3*pid + ((rhsShift+0)%3)*p_pmlNfields;
        const int pmlrhsId2 = 3*pid + ((rhsShift+1)%3)*p_pmlNfields;
        const int pmlrhsId3 = 3*pid + ((rhsShift+2)%3)*p_pmlNfields;

        const dfloat px = pmlq[pid+0] + dt*(a1*pmlrhsq[pmlrhsId1+0] + a2*pmlrhsq[pmlrhsId2+0] + a3*pmlrhsq[pmlrhsId3+0]);
        const dfloat py = pmlq[pid+1] + dt*(a1*pmlrhsq[pmlrhsId1+1] + a2*pmlrhsq[pmlrhsId2+1] + a3*pmlrhsq[pmlrhsId3+1]);

        s_q[p_Nfields*n+0] = q[id+0] + dt*(a1*rhsq[rhsId1+0] + a2*rhsq[rhsId2+0] + a3*rhsq[rhsId3+0]);
        s_q[p_Nfields*n+1] = q[id+1] + dt*(a1*rhsq[rhsId1+1] + a2*rhsq[rhsId2+1] + a3*rhsq[rhsId3+1]);
        s_q[p_Nfields*n+2] = px+py;
      }
    }
    @barrier("local");

    // Interpolate p to cubature nodes
    for(int n=0;n<p_maxCubNodes;++n;@inner(0)){
      if (n<p_cubNp) {
        const int e = pmlElementIds[et];
        // Prefetch value of c2
        const dfloat c2n = c2[n + e*p_cubNp];

        s_p[n] = 0.f;
        for (int i=0;i<p_Np;++i){
          s_p[n] += cubInterp[n + i*p_cubNp] * s_q[p_Nfields*i+2];
        }
        // Multiply result by wavespeed c2 at cubature node
        s_p[n] *= c2n;
      }
    }
    @barrier("local");

    // Project result back down
    for(int n=0;n<p_maxCubNodes;++n;@inner(0)){
      if (n < p_Np){
        // Project scaled rhs down
        dfloat c2p = 0.f;
        for (int i=0;i<p_cubNp;++i){
          c2p += cubProject[n + i*p_Np] * s_p[i];
        }
        s_q[p_Nfields*n+2] = c2p;
      }
    }
    @barrier("local");

    for(int n=0; n < p_maxCubNodes; ++n; @inner(0)){
      if (n < p_Nfp*p_Nfaces){
        const int e = pmlElementIds[et];
        const int iid = e*p_Nfp*p_Nfaces + n;
        const int qidM = p_Nfields*(vmapM[iid]-e*p_Np);
        const int qid  = p_Nfields*iid;

        fQP[qid+0] = s_q[qidM+0];
        fQP[qid+1] = s_q[qidM+1];
        fQP[qid+2] = s_q[qidM+2];
        fQM[qid+0] = s_q[qidM+0];
        fQM[qid+1] = s_q[qidM+1];
        fQM[qid+2] = s_q[qidM+2];
      }
    }
  }

}


@kernel void acousticsMRABPmlUpdate2D_v0(const int pmlNelements,
           @restrict const  int   *  pmlIds,
           const dfloat dt,
           const dfloat a1,
           const dfloat a2,
           const dfloat a3,
           @restrict const  dfloat *  pmlrhsq,
           @restrict dfloat *  pmlq,
           const int   rhsShift){

  for(int et=0;et<pmlNelements;++et;@outer(0)){  // for all elements
    for(int n=0; n < p_Np; ++n; @inner(0)){
      const int pmlId = pmlIds[et];
      const int id = p_pmlNfields*(pmlId*p_Np + n);

      const int rhsId1 = 3*id + ((rhsShift+0)%3)*p_pmlNfields;
      const int rhsId2 = 3*id + ((rhsShift+1)%3)*p_pmlNfields;
      const int rhsId3 = 3*id + ((rhsShift+2)%3)*p_pmlNfields;

      dfloat rhsutilde = dt*(a1*pmlrhsq[rhsId1+0] + a2*pmlrhsq[rhsId2+0] + a3*pmlrhsq[rhsId3+0]);
      dfloat rhsvtilde = dt*(a1*pmlrhsq[rhsId1+1] + a2*pmlrhsq[rhsId2+1] + a3*pmlrhsq[rhsId3+1]);
      dfloat rhsptilde = dt*(a1*pmlrhsq[rhsId1+2] + a2*pmlrhsq[rhsId2+2] + a3*pmlrhsq[rhsId3+2]);

      //write update
      pmlq[id+0] += rhsutilde;
      pmlq[id+1] += rhsvtilde;
      pmlq[id+2] += rhsptilde;
    }
  }
}
