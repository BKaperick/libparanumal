/*

The MIT License (MIT)

Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/

#define p_patchNelements (p_Nfaces+1)
#define p_patchNp (p_Np*(p_Nfaces+1))


// experimental overlapping patch solver
@kernel void ellipticPatchGather(const dlong Nelements,
                                @restrict const  dlong   *  EToE,
                                @restrict const  int   *  EToF,
                                @restrict const  dfloat *  q,
                                @restrict dfloat *  Gq){

  // assume one patch per outer iteration (tune later)
  for(dlong eo=0;eo<Nelements;eo+=p_NblockP;@outer(0)){

    for(dlong e=eo;e<eo+p_NblockP;++e;@inner(1)){
      for(int n=0;n<p_Np;++n;@inner(0)){

        if(e<Nelements){

          dfloat Gqn = 0;

          Gqn = q[p_patchNp*e+n];

          for(int f=0;f<p_Nfaces;++f){

            const dlong eP = EToE[e*p_Nfaces+f]; // load element e first
            const int   fP = EToF[e*p_Nfaces+f];

            if(eP>=0){
              Gqn += q[eP*p_patchNp+(fP+1)*p_Np+n];
            }
          }

          Gq[e*p_Np+n] = Gqn;
        }
      }
    }
  }
}

@kernel void ellipticFacePatchGather(const dlong Nelements,
                                    @restrict const  dlong  *  EToFPairs,
                                    @restrict const  dfloat *  q,
                                    @restrict dfloat *  Gq){

  // assume one patch per outer iteration (tune later)
  for(dlong eo=0;eo<Nelements;eo+=p_NblockP;@outer(0)){

    for(dlong e=eo;e<eo+p_NblockP;++e;@inner(1)){
      for(int n=0;n<p_Np;++n;@inner(0)){

        if(e<Nelements){

          dfloat Gqn = 0;

          for(int f=0;f<p_Nfaces;++f){
            const dlong fP = EToFPairs[p_Nfaces*e+f];

            if (fP >=0) {
              Gqn += q[fP*p_Np + n];
            }
          }

          Gq[e*p_Np+n] = Gqn;
        }
      }
    }
  }
}


