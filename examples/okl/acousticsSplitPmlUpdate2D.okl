/*

The MIT License (MIT)

Copyright (c) 2017 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/


@kernel void acousticsSplitPmlUpdate2D(const int Ntotal,
					  const int recombine,
					  const dfloat dt,	
					  const dfloat rka,
					  const dfloat rkb,
					  @restrict const  dfloat *  rhsqx,
					  @restrict const  dfloat *  rhsqy,
					  @restrict dfloat *  resqx,
					  @restrict dfloat *  resqy,
					  @restrict dfloat *  qx,
					  @restrict dfloat *  qy,
					  @restrict dfloat *  q){
  
  // Low storage Runge Kutta time step update
  for(int n=0;n<Ntotal;++n;@tile(256,@outer,@inner)){
    if(n<Ntotal){
      dfloat resqxn = resqx[n];
      dfloat qxn = qx[n];
      resqxn = rka*resqxn + dt*rhsqx[n];
      resqx[n] = resqxn;
      qxn += rkb*resqxn;

      dfloat resqyn = resqy[n];
      dfloat qyn = qy[n];
      resqyn = rka*resqyn + dt*rhsqy[n];
      resqy[n] = resqyn;
      qyn += rkb*resqyn;
      
      // need to map between qx, qy storage and q storage
      int node  = n%p_Np;
      int field = (n/p_Np)%p_Nfields;
      int element = n/(p_Np*p_Nfields);
      int id = field + node*p_Nfields + element*p_Np*p_Nfields;
      dfloat sumq = qxn+qyn;
      q[id] = sumq + (field==0)*p_q1bar + (field==1)*p_q2bar + (field==2)*p_q3bar;

      if(recombine == 1){
	qxn = 0.5f*sumq;
	qyn = 0.5f*sumq;
      }
	
      qx[n] = qxn;
      qy[n] = qyn;
      
    }
  }
}

