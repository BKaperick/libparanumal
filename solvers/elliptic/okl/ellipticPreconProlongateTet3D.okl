
kernel void ellipticPreconProlongateTet3D(const dlong Nelements,
                                     const dfloat * restrict R,
                                     const dfloat * restrict qCoarse,
                                     dfloat * restrict qFine){

  for(dlong eo=0;eo<Nelements;eo+=p_NblockVFine;outer0){

    shared dfloat s_qCoarse[p_NblockVFine][p_NpCoarse];

    for(int es=0;es<p_NblockVFine;++es;inner1){
      for(int n=0;n<p_NpFine;++n;inner0){
        dlong t = n + es*p_NpFine;

        if(t<p_NpCoarse*p_NblockVFine)
          if((eo*p_NpCoarse + t)<Nelements*p_NpCoarse)
            s_qCoarse[0][t] = qCoarse[eo*p_NpCoarse+t];
      }
    }

    barrier(localMemFence);

    for(int es=0;es<p_NblockVFine;++es;inner1){
      for(int n=0;n<p_NpFine;++n;inner0){
        const dlong e = eo + es;
        if(e<Nelements){
          //dfloat tmp = 0.;
          dfloat tmp = qFine[e*p_NpFine+n];

          occaUnroll(p_NpCoarse)
            for(int i=0;i<p_NpCoarse;++i){
              tmp += R[i*p_NpFine + n]*s_qCoarse[es][i];
            }

          qFine[e*p_NpFine+n] = tmp;
        }
      }
    }
  }
}
